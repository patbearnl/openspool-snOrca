# https://cmake.org/cmake/help/latest/variable/MSVC_VERSION.html
if (MSVC_VERSION EQUAL 1800)
# 1800      = VS 12.0 (v120 toolset)
    set(DEP_VS_VER "12")
    set(DEP_BOOST_TOOLSET "msvc-12.0")
elseif (MSVC_VERSION EQUAL 1900)
# 1900      = VS 14.0 (v140 toolset)    
    set(DEP_VS_VER "14")
    set(DEP_BOOST_TOOLSET "msvc-14.0")
elseif (MSVC_VERSION LESS 1920)
# 1910-1919 = VS 15.0 (v141 toolset)
    set(DEP_VS_VER "15")
    set(DEP_BOOST_TOOLSET "msvc-14.1")
elseif (MSVC_VERSION LESS 1930)
# 1920-1929 = VS 16.0 (v142 toolset)
    set(DEP_VS_VER "16")
    set(DEP_BOOST_TOOLSET "msvc-14.2")
elseif (MSVC_VERSION LESS 1950)
# 1930-1949 = VS 17.0 (v143 toolset)
    set(DEP_VS_VER "17")
    set(DEP_BOOST_TOOLSET "msvc-14.3")
elseif (MSVC_VERSION LESS 1960)
# 1950-1959 = VS 18.0 (Dev18 / VS 2026)
    set(DEP_VS_VER "18")
    set(DEP_BOOST_TOOLSET "msvc-14.5")
else ()
    message(FATAL_ERROR "Unsupported MSVC version")
endif ()

if (CMAKE_CXX_COMPILER_ID STREQUAL Clang)
    set(DEP_BOOST_TOOLSET "clang-win")
endif ()

if (DEP_VS_VER EQUAL 12)
    set(DEP_MSVC_GEN "Visual Studio 12 2013")
elseif (DEP_VS_VER EQUAL 14)
    set(DEP_MSVC_GEN "Visual Studio 14 2015")
elseif (DEP_VS_VER EQUAL 15)
    set(DEP_MSVC_GEN "Visual Studio 15 2017")
elseif (DEP_VS_VER EQUAL 16)
    set(DEP_MSVC_GEN "Visual Studio 16 2019")
elseif (DEP_VS_VER EQUAL 17)
    set(DEP_MSVC_GEN "Visual Studio 17 2022")
elseif (DEP_VS_VER EQUAL 18)
    set(DEP_MSVC_GEN "Visual Studio 18 2026")
else ()
    set(DEP_MSVC_GEN "Visual Studio ${DEP_VS_VER}")
endif ()

if ("${DEPS_ARCH}" STREQUAL "x86")
    set(DEP_PLATFORM "Win32")
elseif ("${DEPS_ARCH}" STREQUAL "x64")
    set(DEP_PLATFORM "x64")
else ()
    message(FATAL_ERROR "Unsupported OS architecture")
endif ()

if (${DEP_DEBUG})
    set(DEP_BOOST_DEBUG "debug")
else ()
    set(DEP_BOOST_DEBUG "")
endif ()

macro(add_debug_dep _dep)
if (${DEP_DEBUG})
    ExternalProject_Get_Property(${_dep} BINARY_DIR)
    ExternalProject_Add_Step(${_dep} build_debug
        DEPENDEES build
        DEPENDERS install
        COMMAND msbuild /m /P:Configuration=Debug INSTALL.vcxproj
        WORKING_DIRECTORY "${BINARY_DIR}"
    )
endif ()
endmacro()

if ("${DEPS_ARCH}" STREQUAL "x86")
    set(DEP_WXWIDGETS_TARGET "")
    set(DEP_WXWIDGETS_LIBDIR "vc_lib")
elseif ("${DEPS_ARCH}" STREQUAL "x64")
    set(DEP_WXWIDGETS_TARGET "TARGET_CPU=X64")
    set(DEP_WXWIDGETS_LIBDIR "vc_x64_lib")
else ()
    message(FATAL_ERROR "Unsupported OS architecture")
endif ()

find_package(Git REQUIRED)

# Newer CMake versions (4.x) removed compatibility with cmake_minimum_required() < 3.5.
# Some third-party deps still declare older minimums, so request CMake to emulate 3.5-era policies.
set(DEP_CMAKE_OPTS "-DCMAKE_POLICY_VERSION_MINIMUM=3.5")
